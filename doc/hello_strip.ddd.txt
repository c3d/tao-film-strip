import Animate     // For the [A..B, ] notation
import FilmStrip

key "Return" -> goto_page page_name 1

page "Film strip",
    GradientBackground

    [2..10, ]
        StripTransform smooth_down_ratio

        color "white"
        film_strip 25 * down_ratio^2,
            frame_texture 1280, 768,
                HelloWorld
            color "white"
            rounded_rectangle 0, 0, 1120, 672, 40

GradientBackground ->
  locally
    disable_depth_mask
    color "white"
    linear_gradient window_width/2, 0, window_width/2, window_height, window_width, window_height,
        gradient_color 0, 0.8, 0.8, 0.8, 1
        gradient_color 1, 0.2, 0.2, 0.2, 1
    rectangle window_width, window_height

StripTransform R:real ->
        translate 500 * R, 0, -10000 * R
        rotatey -45 * R
        scale 1.0 - R

HelloWorld ->
    color "white"
    milkyway 10000
    rotatez -23
    earth 400
    hello_world 440

milkyway R ->
    locally
        texture_wrap true, true
        texture_transform {scale 5, 5, 5}
        texture "system:templates/hello_world/images/milkyway.jpg"
        rotatey 0.02 * page_time + 100
        scale 1, -1, 1
        sphere R

earth R ->
    locally
        texture "system:templates/hello_world/images/earth.jpg"
        rotatey 5 * page_time + 250
        sphere 0, 0, 0, R

hello_world R ->
    locally
        frame_texture 1900, 600,
            color 1, 1, 1, 1
            reset_transform
            font "Arial Unicode MS", "unifont", 72
            move_to -800, -9, 0
            text "Hello World! or Καλημέρα κόσμε; or こんにちは 世界"
        rotatey -11 * page_time + 180
        color 20% , 20% , 20% , 70%
        sphere 0, 0, 0, R - 30
        color 100% , 90% , 20% , 90%
        sphere 0, 0, 0, R

